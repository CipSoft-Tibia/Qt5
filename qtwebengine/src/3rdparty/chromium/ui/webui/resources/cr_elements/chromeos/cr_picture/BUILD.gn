# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../os_cr_elements.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

js_type_check("closure_compile") {
  deps = [
    ":cr_camera",
    ":cr_picture_list",
    ":cr_picture_pane",
    ":cr_picture_types",
    ":png",
  ]
}

js_library("cr_camera") {
  deps = [ ":png" ]
}

js_library("cr_picture_list") {
  deps = [
    ":cr_picture_types",
    ":png",
    "//third_party/polymer/v1_0/components-chromium/iron-selector:iron-selector-extracted",
    "//ui/webui/resources/js:assert",
  ]
}

js_library("cr_picture_pane") {
  deps = [
    ":cr_camera",
    ":cr_picture_types",
    ":png",
  ]
}

js_library("cr_picture_types") {
}

js_library("png") {
  deps = [ "//ui/webui/resources/js:cr" ]
}

js_modulizer("modulize") {
  input_files = [
    "png.js",
    "cr_picture_types.js",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":cr_camera.m",
    ":cr_picture_list.m",
    ":cr_picture_pane.m",
    ":png.m",
  ]
}

js_library("png.m") {
  # Need to turn off default sources filtering by GN, otherwise |sources| is
  # filtered on non-CrOS, because it contains the term "chromeos". This
  # js_library() target is needed in non-CrOS builds as well (see
  # chrome/browser/resources/settings/people_page:people_page.m)
  set_sources_assignment_filter([])
  sources = [
    "$root_gen_dir/ui/webui/resources/cr_elements/chromeos/cr_picture/png.m.js",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("cr_picture_types.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/chromeos/cr_picture/cr_picture_types.m.js" ]
  extra_deps = [ ":modulize" ]
}

group("polymer3_elements") {
  public_deps = [
    ":cr_camera_module",
    ":cr_picture_list_module",
    ":cr_picture_pane_module",
    ":icons_module",
    ":modulize",
  ]
}

js_library("cr_picture_pane.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/chromeos/cr_picture/cr_picture_pane.m.js" ]
  deps = [
    ":cr_camera.m",
    ":cr_picture_types.m",
    ":png.m",
  ]
  extra_deps = [ ":cr_picture_pane_module" ]
}

js_library("cr_camera.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/chromeos/cr_picture/cr_camera.m.js" ]
  deps = [ ":png.m" ]
  extra_deps = [ ":cr_camera_module" ]
}

js_library("cr_picture_list.m") {
  sources = [ "$root_gen_dir/ui/webui/resources/cr_elements/chromeos/cr_picture/cr_picture_list.m.js" ]
  deps = [
    ":cr_picture_types.m",
    ":png.m",
    "//third_party/polymer/v3_0/components-chromium/iron-selector:iron-selector",
    "//ui/webui/resources/js:assert.m",
  ]
  extra_deps = [ ":cr_picture_list_module" ]
}

polymer_modulizer("cr_camera") {
  js_file = "cr_camera.js"
  html_file = "cr_camera.html"
  html_type = "dom-module"
  namespace_rewrites = cr_elements_chromeos_namespace_rewrites
  auto_imports = cr_elements_chromeos_auto_imports
}

polymer_modulizer("cr_picture_list") {
  js_file = "cr_picture_list.js"
  html_file = "cr_picture_list.html"
  html_type = "dom-module"
  namespace_rewrites = cr_elements_chromeos_namespace_rewrites
  auto_imports = cr_elements_chromeos_auto_imports
}

polymer_modulizer("cr_picture_pane") {
  js_file = "cr_picture_pane.js"
  html_file = "cr_picture_pane.html"
  html_type = "dom-module"
  namespace_rewrites = cr_elements_chromeos_namespace_rewrites
  auto_imports = cr_elements_chromeos_auto_imports
}

polymer_modulizer("icons") {
  js_file = "icons.m.js"
  html_file = "icons.html"
  html_type = "iron-iconset"
}
