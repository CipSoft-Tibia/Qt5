# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

source_set("input_monitor") {
  public = [ "local_input_monitor.h" ]

  sources = [
    "local_hotkey_input_monitor.h",
    "local_hotkey_input_monitor_chromeos.cc",
    "local_hotkey_input_monitor_mac.mm",
    "local_hotkey_input_monitor_win.cc",
    "local_hotkey_input_monitor_x11.cc",
    "local_input_monitor.cc",
    "local_input_monitor_win.cc",
    "local_input_monitor_win.h",
    "local_keyboard_input_monitor.h",
    "local_keyboard_input_monitor_chromeos.cc",
    "local_keyboard_input_monitor_mac.mm",
    "local_keyboard_input_monitor_win.cc",
    "local_keyboard_input_monitor_x11.cc",
    "local_mouse_input_monitor_mac.mm",
    "local_mouse_input_monitor_win.cc",
    "local_mouse_input_monitor_x11.cc",
    "local_pointer_input_monitor.h",
    "local_pointer_input_monitor_chromeos.cc",
  ]

  deps = [
    "//remoting/proto",
    "//third_party/webrtc_overrides:webrtc_component",
    "//ui/events",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }

  if (is_mac) {
    deps += [ "//third_party/google_toolbox_for_mac" ]
  }

  if (!use_x11 || (is_chromeos && !use_ozone)) {
    sources -= [
      "local_hotkey_input_monitor_x11.cc",
      "local_keyboard_input_monitor_x11.cc",
      "local_mouse_input_monitor_x11.cc",
    ]
  }
}
