# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Projector Annotator is ChromeOS only")

js_library("trusted_annotator_library") {
  sources = [
    "annotator_browser_proxy.js",
    "annotator_embedder_impl.js",
    "trusted_annotator_comm_factory.js",
  ]
  deps = [
    "//ash/webui/common/resources:cr.m",
    "//ash/webui/common/resources:cr_deprecated",
    "//ash/webui/common/resources:web_ui_listener_behavior",
    "//ash/webui/common/resources/post_message_api:post_message_api_client",
    "//ash/webui/common/resources/post_message_api:post_message_api_request_handler",
    "//ash/webui/projector_app/resources/common:message_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  externs_list = [
    "$externs_path/chrome_send.js",
    "../../common/projector_app.externs.js",
  ]
}

generate_grd("build_trusted_grd") {
  input_files = [
    "annotator_embedder.html",
    "annotator_embedder.css",
    "annotator_browser_proxy.js",
    "trusted_annotator_comm_factory.js",
    "annotator_embedder_impl.js",
  ]

  input_files_base_dir = rebase_path(".", "//")
  grd_prefix = "ash_projector_annotator_trusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}
