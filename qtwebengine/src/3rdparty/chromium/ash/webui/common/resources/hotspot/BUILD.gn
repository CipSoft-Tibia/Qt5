# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Hotspot is Chrome OS only.")

preprocessed_dir = "$target_gen_dir/preprocessed"

ts_files = [
  "cros_hotspot_config.ts",
  "fake_hotspot_config.ts",
]

preprocess_if_expr("preprocess") {
  in_folder = "."
  in_files = ts_files
  out_folder = preprocessed_dir
}

copy("copy_files") {
  deps =
      [ "//chromeos/ash/services/hotspot_config/public/mojom:mojom_webui_js" ]
  sources = [ "$root_gen_dir/mojom-webui/chromeos/ash/services/hotspot_config/public/mojom/cros_hotspot_config.mojom-webui.js" ]
  outputs = [ "$preprocessed_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  allow_js = true
  root_dir = preprocessed_dir
  out_dir = "$root_gen_dir/ash/webui/common/resources/preprocessed/hotspot"
  in_files = ts_files + [ "cros_hotspot_config.mojom-webui.js" ]
  composite = true
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  deps = [
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:library",
  ]
  extra_deps = [
    ":copy_files",
    ":preprocess",
    "//ash/webui/common/resources:copy_checked_in_dts_files",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "ash_common_hotspot"
  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  out_grd = "$target_gen_dir/resources.grdp"
  resource_path_prefix = "ash/common/hotspot"
}
