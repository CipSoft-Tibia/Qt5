# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_ios) {
  import("//build/config/ios/bundle_data_from_filelist.gni")
}

component("common") {
  output_name = "webdata_common"

  sources = [
    "web_data_request_manager.cc",
    "web_data_request_manager.h",
    "web_data_results.h",
    "web_data_service_base.cc",
    "web_data_service_base.h",
    "web_data_service_consumer.cc",
    "web_data_service_consumer.h",
    "web_database.cc",
    "web_database.h",
    "web_database_backend.cc",
    "web_database_backend.h",
    "web_database_service.cc",
    "web_database_service.h",
    "web_database_table.cc",
    "web_database_table.h",
    "webdata_constants.cc",
    "webdata_constants.h",
    "webdata_export.h",
  ]

  defines = [ "WEBDATA_IMPLEMENTATION" ]

  deps = [
    "//base",
    "//sql",
  ]
}

if (is_ios) {
  bundle_data_from_filelist("unit_tests_bundle_data") {
    visibility = [ ":unit_tests" ]
    testonly = true
    filelist_name = "unit_tests_bundle_data.filelist"
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "web_database_migration_unittest.cc" ]

  deps = [
    ":common",
    "//base",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/password_manager/core/browser",
    "//components/search_engines",
    "//components/signin/public/webdata",
    "//sql",
    "//testing/gtest",
  ]
  if (is_ios) {
    deps += [ ":unit_tests_bundle_data" ]
  }
}
