# Copyright 2015 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//printing/buildflags/buildflags.gni")

assert(enable_printing)

source_set("common") {
  sources = [
    "cloud_print_cdd_conversion.cc",
    "cloud_print_cdd_conversion.h",
    "print_params.cc",
    "print_params.h",
  ]

  deps = [
    ":mojo_interfaces",
    "//base",
    "//build:chromeos_buildflags",
    "//components/cloud_devices/common:common",
    "//printing",
    "//printing/backend",
    "//printing/common",
    "//printing/mojom",
  ]
  if (is_qtwebengine) {
    sources -= [
      "cloud_print_cdd_conversion.cc",
      "cloud_print_cdd_conversion.h",
    ]
    deps -= [
      "//components/cloud_devices/common:common",
    ]
  }
}

mojom("mojo_interfaces") {
  sources = [ "print.mojom" ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//printing/mojom",
    "//ui/gfx/geometry/mojom",
  ]

  enabled_features = []
  if (enable_print_preview) {
    enabled_features += [ "enable_print_preview" ]
  }
  if (enable_tagged_pdf) {
    assert(enable_print_preview)
    public_deps += [ "//ui/accessibility/mojom" ]
    enabled_features += [ "enable_tagged_pdf" ]
  }
  if (enable_print_content_analysis) {
    enabled_features += [ "enable_print_content_analysis" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "cloud_print_cdd_conversion_unittest.cc" ]
  deps = [
    ":common",
    "//base/test:test_support",
    "//printing/backend",
    "//printing/backend:test_support",
    "//testing/gtest",
    "//ui/gfx/geometry:geometry",
  ]
}
