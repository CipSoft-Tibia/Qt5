# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

if (is_android) {
  import("//build/config/android/rules.gni")  # For generate_jni().
}

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

source_set("screen_orientation") {
  visibility = [ "//services/device:lib" ]

  sources = [
    "screen_orientation_listener_android.cc",
    "screen_orientation_listener_android.h",
  ]

  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
  ]

  public_deps = [ "//services/device/public/mojom" ]

  if (is_android) {
    deps += [ ":screen_orientation_jni_headers" ]
  }
}

if (is_android) {
  generate_jni("screen_orientation_jni_headers") {
    visibility = [ ":screen_orientation" ]
    sources = [ "android/java/src/org/chromium/device/screen_orientation/ScreenOrientationListener.java" ]
  }

  android_library("java") {
    # Conceptually, this should be visible only to //services/device:java.
    # However, various generated targets also need to see this target as a
    # result of //services/device:java depending on it.
    visibility = [ "//services/device:*" ]
    sources = [ "android/java/src/org/chromium/device/screen_orientation/ScreenOrientationListener.java" ]
    deps = [
      "//base:base_java",
      "//ui/android:ui_no_recycler_view_java",
    ]
  }
}
