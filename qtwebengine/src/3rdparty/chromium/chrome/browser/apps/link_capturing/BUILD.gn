# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("link_capturing") {
  sources = [
    "link_capturing_navigation_throttle.cc",
    "link_capturing_navigation_throttle.h",
  ]

  deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/web_applications",
    "//components/keep_alive_registry",
    "//components/page_load_metrics/browser",
    "//content/public/browser",
    "//third_party/abseil-cpp:absl",
    "//url",
  ]

  if (enable_extensions) {
    deps += [ "//extensions/common" ]
  }

  if (is_chromeos) {
    sources += [
      "chromeos_link_capturing_delegate.cc",
      "chromeos_link_capturing_delegate.h",
    ]
    deps += [
      "//ash/webui/projector_app/public/cpp",
      "//chrome/browser/apps/app_service",
    ]
  } else {
    sources += [
      "web_app_link_capturing_delegate.cc",
      "web_app_link_capturing_delegate.h",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "link_capturing_navigation_throttle_unittest.cc" ]

  deps = [
    ":link_capturing",
    "//testing/gtest",
    "//ui/base:base",
    "//url",
  ]
}
