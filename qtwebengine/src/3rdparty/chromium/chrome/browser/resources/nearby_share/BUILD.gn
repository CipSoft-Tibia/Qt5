# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_js.gni")

assert(is_chromeos, "Nearby Share is CrOS only")

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags =
      default_closure_args + [
        "js_module_root=../../chrome/browser/resources/nearby_share/",
        "js_module_root=./gen/chrome/browser/resources/nearby_share/",
      ]
  deps = [
    ":app",
    ":discovery_manager",
    ":nearby_confirmation_page",
    ":nearby_device",
    ":nearby_device_icon",
    ":nearby_discovery_page",
    ":nearby_preview",
    ":nearby_progress",
  ]
}

js_library("app") {
  deps = [
    ":nearby_confirmation_page",
    ":nearby_discovery_page",
    "./shared:nearby_onboarding_page.m",
    "./shared:nearby_share_settings_behavior.m",
    "./shared:nearby_visibility_page.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_view_manager:cr_view_manager.m",
  ]
  extra_deps = [ "./shared:modulize" ]
}

js_library("discovery_manager") {
  deps =
      [ "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile" ]
}

js_library("nearby_confirmation_page") {
  deps = [
    ":nearby_preview",
    ":nearby_progress",
    "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_checkbox:cr_checkbox.m",
    "//ui/webui/resources/js:i18n_behavior.m",
  ]
}

js_library("nearby_device") {
  deps = [
    ":nearby_device_icon",
    "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/iron-icon",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_device_icon") {
  deps = [
    "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/iron-icon",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_discovery_page") {
  deps = [
    ":discovery_manager",
    ":nearby_device",
    ":nearby_preview",
    "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/iron-list",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/js:assert.m",
  ]
}

js_library("nearby_preview") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_progress") {
  deps = [
    ":nearby_device_icon",
    "//chrome/browser/ui/webui/nearby_share:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

html_to_js("web_components") {
  js_files = [
    "app.js",
    "icons.js",
    "nearby_confirmation_page.js",
    "nearby_device.js",
    "nearby_device_icon.js",
    "nearby_discovery_page.js",
    "nearby_preview.js",
    "nearby_progress.js",
  ]
}
