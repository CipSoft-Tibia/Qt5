# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

static_library("test_support") {
  testonly = true
  sources = [
    "mock_node_channel_delegate.cc",
    "mock_node_channel_delegate.h",
    "mojo_test_base.cc",
    "mojo_test_base.h",
    "test_utils.h",
    "test_utils_win.cc",
  ]

  if (!is_ios) {
    sources += [
      "multiprocess_test_helper.cc",
      "multiprocess_test_helper.h",
    ]
  }

  if (is_fuchsia || is_posix) {
    sources += [ "test_utils.cc" ]
  }

  public_deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/core:embedder_internal",
    "//mojo/core/embedder",
    "//mojo/public/cpp/system",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("run_all_unittests") {
  testonly = true
  sources = [ "run_all_unittests.cc" ]

  deps = [
    ":test_support",
    ":test_support_impl",
    "//base",
    "//base/test:test_support",
    "//mojo/core/embedder",
    "//mojo/public/c/test_support",
    "//testing/gtest",
  ]

  if ((is_linux || is_chromeos) && !is_component_build) {
    public_configs = [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

source_set("run_all_perftests") {
  testonly = true
  deps = [
    ":test_support_impl",
    "//base",
    "//base/test:test_support",
    "//mojo/core/embedder",
    "//mojo/core/test:test_support",
    "//mojo/public/c/test_support",
  ]

  sources = [ "run_all_perftests.cc" ]

  if ((is_linux || is_chromeos) && !is_component_build) {
    public_configs = [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

static_library("test_support_impl") {
  testonly = true
  deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/public/c/test_support",
    "//mojo/public/cpp/system",
  ]

  sources = [
    "test_support_impl.cc",
    "test_support_impl.h",
  ]
}

if (is_mac) {
  proto_library("channel_mac_proto") {
    proto_in_dir = "//"
    sources = [ "data/channel_mac/channel_mac.proto" ]
    link_deps = [ "//testing/libfuzzer/fuzzers/mach:proto" ]
  }
}
