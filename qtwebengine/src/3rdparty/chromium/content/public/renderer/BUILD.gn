# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/jumbo.gni")
import("//media/media_options.gni")
import("//ppapi/buildflags/buildflags.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

# See //content/BUILD.gn for how this works.
group("renderer") {
  if (is_component_build) {
    public_deps = [ "//content" ]
  } else {
    public_deps = [ ":renderer_sources" ]
  }
}

if (is_component_build) {
  link_target_type = "jumbo_source_set"
} else {
  link_target_type = "jumbo_static_library"
}
target(link_target_type, "renderer_sources") {
  # External code should depend on via ":renderer" above.
  visibility = [ "//content/*" ]

  sources = [
    "chrome_object_extensions_utils.cc",
    "chrome_object_extensions_utils.h",
    "content_renderer_client.cc",
    "content_renderer_client.h",
    "context_menu_client.h",
    "document_state.cc",
    "document_state.h",
    "pepper_plugin_instance.h",
    "plugin_ax_tree_source.h",
    "plugin_instance_throttler.h",
    "render_accessibility.h",
    "render_frame.h",
    "render_frame_media_playback_options.h",
    "render_frame_observer.cc",
    "render_frame_observer.h",
    "render_frame_observer_tracker.h",
    "render_frame_visitor.h",
    "render_thread.cc",
    "render_thread.h",
    "render_thread_observer.cc",
    "render_thread_observer.h",
    "render_view.h",
    "render_view_observer.cc",
    "render_view_observer.h",
    "render_view_observer_tracker.h",
    "render_view_visitor.h",
    "renderer_ppapi_host.h",
    "request_peer.h",
    "resource_dispatcher_delegate.h",
    "seccomp_sandbox_status_android.h",
    "url_loader_throttle_provider.h",
    "v8_value_converter.h",
    "video_encode_accelerator.cc",
    "video_encode_accelerator.h",
    "websocket_handshake_throttle_provider.h",
    "window_features_converter.cc",
    "window_features_converter.h",
    "worker_thread.h",
  ]

  configs += [ "//content:content_implementation" ]

  public_deps = [ "//media" ]

  deps = [
    "//content/public/child:child_sources",
    "//content/public/common:common_sources",
    "//content/renderer",
    "//gin",
    "//media/capture",
    "//media/gpu/ipc/client",
    "//net",
    "//ppapi/buildflags",
    "//ppapi/c",
    "//skia",
    "//third_party/blink/public:blink_headers",
    "//third_party/blink/public/common",
    "//third_party/widevine/cdm:headers",
    "//ui/base",
    "//ui/base/ime",
    "//ui/gfx",
    "//v8",
  ]

  allow_circular_includes_from = [
    # This target is a pair with content/renderer. They always go together and
    # include headers from each other.
    "//content/renderer",
  ]

  if (is_android) {
    # For seccomp_sandbox_status_android.h.
    deps += [ "//sandbox" ]
  }

  if (enable_webrtc) {
    deps += [
      "//third_party/webrtc_overrides:webrtc_component",
    ]
  }

  if (enable_library_cdms) {
    sources += [
      "key_system_support.cc",
      "key_system_support.h",
    ]
  }
}
