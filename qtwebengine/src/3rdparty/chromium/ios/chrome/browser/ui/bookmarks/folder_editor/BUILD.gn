# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Public target.
group("folder_editor") {
  public_deps = [ ":coordinator_headers" ]
  deps = [ ":coordinator_impl" ]
}

# Do not include this target. Prefer :folder_editor instead.
# This target is only used for internal builds in bookmark.
# Contains coordinator headers.
source_set("coordinator_headers") {
  configs += [ "//build/config/compiler:enable_arc" ]
  visibility = [
    ":coordinator_impl",
    ":folder_editor",
    "//ios/chrome/browser/ui/bookmarks/folder_chooser:coordinator_impl",
  ]
  sources = [
    "bookmarks_folder_editor_coordinator.h",
    "bookmarks_folder_editor_coordinator_delegate.h",
  ]
  public_deps = [ "//ios/chrome/browser/ui/coordinators:chrome_coordinators" ]
}

# Do not include this target. Prefer :folder_editor instead.
# This target is only used for internal builds in bookmark.
# Contains coordinator implementation.
source_set("coordinator_impl") {
  configs += [ "//build/config/compiler:enable_arc" ]
  visibility = [ ":folder_editor" ]
  sources = [ "bookmarks_folder_editor_coordinator.mm" ]
  deps = [
    ":coordinator_headers",
    ":ui",
    "//base",
    "//components/bookmarks/browser",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/ui/bookmarks:core",
    "//ios/chrome/browser/ui/bookmarks/folder_chooser:coordinator_headers",
    "//ios/chrome/browser/ui/commands",
  ]
}

# Private to this coordinator.
# Contains folder editor ui implementation.
source_set("ui") {
  configs += [ "//build/config/compiler:enable_arc" ]
  visibility = [ ":coordinator_impl" ]
  sources = [
    "bookmarks_folder_editor_view_controller.h",
    "bookmarks_folder_editor_view_controller.mm",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//components/bookmarks/browser",
    "//ios/chrome/app/strings:ios_strings_grit",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/ui/alert_coordinator",
    "//ios/chrome/browser/ui/bookmarks:constants",
    "//ios/chrome/browser/ui/bookmarks:core",
    "//ios/chrome/browser/ui/bookmarks/cells",
    "//ios/chrome/browser/ui/commands",
    "//ios/chrome/browser/ui/icons",
    "//ios/chrome/browser/ui/image_util",
    "//ios/chrome/browser/ui/keyboard",
    "//ios/chrome/browser/ui/table_view:styler",
    "//ios/chrome/browser/ui/table_view:table_view",
    "//ios/chrome/browser/ui/table_view:utils",
    "//ios/chrome/browser/ui/table_view:views",
    "//ios/chrome/common/ui/util",
    "//ui/base",
  ]
}
