# Copyright 2017 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

source_set("device_monitors") {
  # Do not expand the visibility here without double-checking with OWNERS, this
  # is a roll-up target which is part of the //media component. Most other DEPs
  # should be using //media and not directly DEP this roll-up target.
  visibility = [ "//media" ]

  sources = [
    "device_monitor_mac.h",
    "device_monitor_mac.mm",
    "system_message_window_win.cc",
    "system_message_window_win.h",
  ]

  configs += [ "//media:subcomponent_config" ]

  deps = [
    "//base",
    "//media/audio",
    "//media/base",
  ]

  if (use_udev) {
    deps += [ "//device/udev_linux" ]
    sources += [
      "device_monitor_udev.cc",
      "device_monitor_udev.h",
    ]
  }

  if (is_mac) {
    frameworks = [ "Foundation.framework" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "system_message_window_win_unittest.cc" ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//media:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
