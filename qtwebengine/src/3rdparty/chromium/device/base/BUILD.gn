# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//device/vr/buildflags/buildflags.gni")

# This file depends on the legacy global sources assignment filter. It should
# be converted to check target platform before assigning source files to the
# sources variable. Remove this import and set_sources_assignment_filter call
# when the file has been converted. See https://crbug.com/1018739 for details.
import("//build/config/deprecated_default_sources_assignment_filter.gni")
set_sources_assignment_filter(deprecated_default_sources_assignment_filter)

component("base") {
  output_name = "device_base"

  sources = [
    "device_base_export.h",
    "device_info_query_win.cc",
    "device_info_query_win.h",
    "device_monitor_win.cc",
    "device_monitor_win.h",
    "features.cc",
    "features.h",
  ]

  defines = [ "DEVICE_BASE_IMPLEMENTATION" ]

  public_deps = [
    "//base",
    "//device/vr/buildflags",
  ]

  if (use_udev) {
    sources += [
      "device_monitor_linux.cc",
      "device_monitor_linux.h",
    ]

    deps = [ "//device/udev_linux" ]
  }

  if (is_win) {
    libs = [ "setupapi.lib" ]
  }
}
