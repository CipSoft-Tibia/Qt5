#
# Base qmake configuration for INTEGRITY armv8 targets
#
MAKEFILE_GENERATOR      = UNIX

QMAKE_PLATFORM          = integrity

include(unix.conf)

include(ghs-base.conf)

bsp_name = $$(INTEGRITY_BSP)
isEmpty(bsp_name): \
    error("This qmakespec requires $INTEGRITY_BSP to be set")

os_directory = $$(INTEGRITY_DIR)
isEmpty(os_directory): \
    error("This qmakespec requires $INTEGRITY_DIR to be set")

iy_build_target = $$(INTEGRITY_BUILD_TARGET)
isEmpty(iy_build_target): \
    message("This qmakespec requires $INTEGRITY_BUILD_TARGET to be set [dbg|rel|chk|cov] for Integrity versions 11.7.6 and higher")

start_name = $$(INTEGRITY_DIR)/libs/$$(INTEGRITY_BSP)/$$(INTEGRITY_BUILD_TARGET)
rtos_name= libs/$$(INTEGRITY_BSP)/$$(INTEGRITY_BUILD_TARGET)

QMAKE_CC                = cxintarm64 -bsp $$bsp_name -os_dir $$os_directory -non_shared -startfile_dir=$$start_name --rtos_library_directory=$$rtos_name --rtos_library_directory=libs/arm64/$$iy_build_target
QMAKE_CXX               = cxintarm64 -bsp $$bsp_name -os_dir $$os_directory -non_shared -startfile_dir=$$start_name --rtos_library_directory=$$rtos_name --rtos_library_directory=libs/arm64/$$iy_build_target

QMAKE_LINK              = $$QMAKE_CXX
QMAKE_AR                = $$QMAKE_CXX -archive -o

QMAKE_CFLAGS            += -bigswitch
QMAKE_CXXFLAGS          += -bigswitch
QMAKE_LFLAGS            += -bigswitch
